import declassifyutil
import lists

let 
    val pid = self()
    val myLev = `{bob}`
    val myName = "Bob" raisedTo myLev
    val myYear = 2999 raisedTo myLev
    val myGender = false raisedTo myLev
    val myInterests = ["dragons", "bells"] raisedTo myLev
    val myProfile = (myLev, myName, myYear, myGender, myInterests)
    
    fun agent (lev, name, year, gender, interests) = 
        let val olderThanMe = if declassify_with_block(myYear > year, authority, lev) then true else false
            val woman = if gender then true else false
            val match = if olderThanMe andalso woman
                        then (true raisedTo lev, declassifydeep(myProfile, authority, lev))
                        else (false raisedTo lev, ())
        in match
        end
    
    fun loop matches =
        let
            val match = receive [ hn ("NEWMATCH", data) => data]
            val allMatches = append matches [match]
            val _ = map (fn x => print x) allMatches
        in loop allMatches
        end

    fun main () =
        let
            val message = ("NEWPROFILE", (myProfile, agent, pid))
            val datingServer = whereis ("QmSC4Z8K56hLM6n27JhwfyX4K3je8eijaoX5ztmKdNLEc9", "datingServer")
            val _ = send(datingServer, message)
        in loop []
        end

in main ()
end